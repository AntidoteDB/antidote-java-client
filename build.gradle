group 'eu.antidotedb'
version '0.3.3'

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'com.bmuschko.nexus'

// requires 'protoc' to be installed locally
apply plugin: 'com.google.protobuf'

sourceCompatibility = 1.8


buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.8'
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.google.protobuf:protobuf-java:3.8.0'
    compile group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.2'
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

protobuf {
    generatedFilesBaseDir = "$projectDir/gen"
    protoc {
        // Download protoc from repositories
        artifact = 'com.google.protobuf:protoc:3.8.0'
    }
}

javadoc {
    options.overview = "src/main/java/overview.html"
//    options.noNavBar = true
}


sourceSets {
    main {
        java {
            srcDir 'src/main/java'
            srcDir "$protobuf.generatedFilesBaseDir/main/java"
        }
    }
}


clean {
    delete protobuf.generatedFilesBaseDir
}


/**
 * Publishing to maven central:
 *
 * 0. Configure once:
 *
 *    Edit ~/.gradle/gradle.properties and add secret information:
 *
 *          signing.keyId=0E1885EA
 *          signing.password=********
 *          signing.secretKeyRingFile=/path/to/gnupg/secring.gpg
 *
 *          nexusUsername=pzeller
 *          nexusPassword=***********
 *
 *
 * 1. Run ./gradlew uploadArchives
 * 2. Go to https://oss.sonatype.org/ and sign in
 * 3. Go to "Staging Repositories" and find project
 * 4. Close repository
 * 5. Release repository
 *
 */

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
    }
}

modifyPom {
    project {
        name 'Java Antidote Client'
        packaging 'jar'
        // optionally artifactId can be defined here
        description 'A client to use the Antidote database from Java.'
        url 'https://github.com/SyncFree/antidote-java-client/'

        scm {
            connection 'scm:https://github.com/SyncFree/antidote-java-client'
            developerConnection 'scm:https://github.com/SyncFree/antidote-java-client'
            url 'https://github.com/SyncFree/antidote-java-client/'
        }

        licenses {
            license {
                name 'The Apache License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
        }

        developers {
            developer {
                id 'pzeller'
                name 'Peter Zeller'
                email 'p_zeller@cs.uni-kl.de'
            }
        }
    }
}

extraArchive {
    sources = true
    javadoc = true
}
